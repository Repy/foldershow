plugins {
    id 'application'
    id 'org.beryx.jlink' version '3.1.1'
}

group 'info.repy.foldershow'
version '1.0-SNAPSHOT'

java{
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

application {
    mainModule = 'info.repy.foldershow'
    mainClass = 'info.repy.foldershow.Main'
}

compileJava{
    options.encoding = 'UTF-8'
}

jlink {
    options = ['--strip-debug', '--no-header-files', '--no-man-pages', '--add-modules', 'java.base']
    launcher {
        name = 'foldershow'
    }
}

repositories {
    mavenCentral()
}

dependencies {
}

task buildWindows(type: Zip, dependsOn: tasks.jlink) {
    archiveFileName = "foldershow.zip"
    into("image") {
        from fileTree(tasks.jlink.imageDir)
    }
    from "scripts/windows/foldershow.bat"
}

task buildMac(type: Tar, dependsOn: tasks.jlink) {
    compression = Compression.GZIP
    archiveFileName = "foldershow.tar.gz"
    into("TOTPClient.app"){
        from fileTree("scripts/mac")
        into("Contents/MacOS/image"){
            from fileTree(tasks.jlink.imageDir)
        }
    }
}

task buildLinux(type: Tar, dependsOn: tasks.jlink) {
    compression = Compression.GZIP
    archiveFileName = "foldershow.tar.gz"
    into("image") {
        from fileTree(tasks.jlink.imageDir)
    }
    from "scripts/linux/foldershow.sh"
}
